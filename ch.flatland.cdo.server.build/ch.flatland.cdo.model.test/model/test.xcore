@GenModel(featureDelegation="Dynamic",
	rootExtendsClass="org.eclipse.emf.internal.cdo.CDOObjectImpl",
	rootExtendsInterface="org.eclipse.emf.cdo.CDOObject",
	modelPluginVariables="org.eclipse.xtext.xbase.lib org.eclipse.emf.ecore.xcore.lib org.eclipse.emf.cdo",
	childCreationExtenders="true",
	editDirectory="/ch.flatland.cdo.model.test.edit/src",
	modelDirectory="/ch.flatland.cdo.model.test/src",
	creationIcons="false")
package ch.flatland.cdo.model.test

import org.eclipse.emf.ecore.xml.^type.Base64Binary

annotation "http://www.eclipse.org/CDO/DBStore" as DBStore

class SimpleDataTypes {
	String testString
	boolean testBoolean
	Boolean testBooleanObject
	int testInt
	Integer testIntObject
	long testLong
	Long testLongObject
	short testShort
	Short testShortObject
	double testDouble
	Double testDoubleObject
	float testFloat
	Float testFloatObject
	byte testByte
	Byte testByteObject
	char testChar
	Character testCharObject
	Date testDate
	BigDecimal testBigDecimal
	BigInteger testBigInteger
	TestEnum testEnum
}

class SimpleDataTypesAsArray {
	String[] testString
	boolean[] testBoolean
	int[] testInt
	long[] testLong
	short[] testShort
	double[] testDouble
	float[] testFloat
	byte[] testByte
	char[] testChar
	Date[] testDate
	BigDecimal[] testBigDecimal
	BigInteger[] testBigInteger
	TestEnum[] testEnum
}

class TestObject {
	String name
	Maxlength8 maxlength8
	refers TestObject singleReference
	refers TestObject[] multiReferences
	refers TestObject[0..2] fixUpperBoundReferences
	refers TestObject[2..2] fixBoundReferences
	refers TestObject[1..2] fixlowerBoundReferences
}

class TestClob {
	@DBStore(columnType="CLOB")
	Base64Binary clob
}

enum TestEnum {
	literal0 = 0
	literal1 = 1
	literal2 = 2
}

@ExtendedMetaData(name="Maxlength8", maxLength="8")
type Maxlength8 wraps String